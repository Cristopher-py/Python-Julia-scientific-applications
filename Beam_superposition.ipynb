{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "a197fd2e",
   "metadata": {},
   "source": [
    "## Superposition principle for beams\n",
    "\n",
    "The superposition principle states that for elastic beams, the total displacement or stress at a point subjected to multiple loads is the sum of the displacements or stresses caused by each load acting independently."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fe5713d1",
   "metadata": {},
   "source": [
    "<div align=\"center\">\n",
    "  <img src=\"Problem_image.jpeg\" alt=\"Alt text\">\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "77a51833",
   "metadata": {},
   "source": [
    "Maxima script below to solve the problem using the superposition principle"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "82ead15b",
   "metadata": {},
   "source": [
    "```maxima\n",
    "\n",
    "(%i2)\t/* ---------------------------- */\n",
    "\t/* PART A: DISTRIBUTED LOAD CASE */\n",
    "\t/* ---------------------------- */\n",
    "\t\n",
    "\t/* 1A: Solve ODE for Segment 1 (distributed load) */\n",
    "\teq: 'diff(u(z), z, 4) = p / (E * I);  /* 4th-order beam equation */\n",
    "\tsol_1A: desolve(eq, u(z));            /* General solution for segment 1A */\n",
    "\t\n",
    "(eq)\t'diff(u(z),z,4)=p/(E*I)\n",
    "(sol_1A)\tu(z)=(z^3*(at('diff(u(z),z,3),z=0)))/6+(z^2*(at('diff(u(z),z,2),z=0)))/2+z*(at('diff(u(z),z,1),z=0))+(p*z^4)/(24*E*I)+\n",
    "u(0)\n",
    "(%i4)\t/* 2A: Solve ODE for Segment 2 (unloaded) */\n",
    "\teq: 'diff(u(z), z, 4) = 0;            /* Homogeneous equation */\n",
    "\tsol_2A: desolve(eq, u(z));            /* General solution for segment 2A */\n",
    "\t\n",
    "(eq)\t'diff(u(z),z,4)=0\n",
    "\n",
    "(sol_2A)\tu(z)=(z^3*(at('diff(u(z),z,3),z=0)))/6+(z^2*(at('diff(u(z),z,2),z=0)))/2+z*(at('diff(u(z),z,1),z=0))+u(0)\n",
    "(%i6)\t/* Apply initial conditions at z=0 */\n",
    "\tsol_1A: rhs(subst([\n",
    "\t    u(0) = c_1,               /* Deflection at z=0 */\n",
    "\t    'diff(u(z), z, 1) = c_2,  /* Slope at z=0 */\n",
    "\t    'diff(u(z), z, 2) = c_3,  /* Moment at z=0 */\n",
    "\t    'diff(u(z), z, 3) = c_4   /* Shear at z=0 */\n",
    "\t], sol_1A));\n",
    "\t\n",
    "\tsol_2A: rhs(subst([\n",
    "\t    u(0) = c_5,               /* Deflection at z=0 */\n",
    "\t    'diff(u(z), z, 1) = c_6,  /* Slope at z=0 */\n",
    "\t    'diff(u(z), z, 2) = c_7,  /* Moment at z=0 */\n",
    "\t    'diff(u(z), z, 3) = c_8   /* Shear at z=0 */\n",
    "\t], sol_2A));\n",
    "\t\n",
    "(sol_1A)\t(p*z^4)/(24*E*I)+(c_4*z^3)/6+(c_3*z^2)/2+c_2*z+c_1\n",
    "(sol_2A)\t(c_8*z^3)/6+(c_7*z^2)/2+c_6*z+c_5\n",
    "(%i14)\t/* Interface continuity at z=Z */\n",
    "\teq1: subst(Z, z, sol_1A) = subst(Z, z, sol_2A);              /* Deflection continuity */\n",
    "\teq2: subst(Z, z, diff(sol_1A, z, 1)) = subst(Z, z, diff(sol_2A, z, 1));  /* Slope continuity */\n",
    "\teq3: subst(Z, z, diff(sol_1A, z, 2)) = subst(Z, z, diff(sol_2A, z, 2));  /* Moment continuity */\n",
    "\teq4: subst(Z, z, diff(sol_1A, z, 3)) = subst(Z, z, diff(sol_2A, z, 3));  /* Shear continuity */\n",
    "\t\n",
    "\t/* Boundary conditions at supports */\n",
    "\teq5: subst(0, z, sol_1A) = 0;               /* Deflection = 0 at left support (z=0) */\n",
    "\teq6: subst(L, z, sol_2A) = 0;               /* Deflection = 0 at right support (z=L) */\n",
    "\teq7: subst(0, z, diff(sol_1A, z, 2)) = 0;   /* Moment = 0 at left support (z=0) */\n",
    "\teq8: subst(L, z, diff(sol_2A, z, 2)) = 0;   /* Moment = 0 at right support (z=L) */\n",
    "\t\n",
    "(eq1)\t(Z^4*p)/(24*E*I)+(Z^3*c_4)/6+(Z^2*c_3)/2+Z*c_2+c_1=(Z^3*c_8)/6+(Z^2*c_7)/2+Z*c_6+c_5\n",
    "(eq2)\t(Z^3*p)/(6*E*I)+(Z^2*c_4)/2+Z*c_3+c_2=(Z^2*c_8)/2+Z*c_7+c_6\n",
    "(eq3)\t(Z^2*p)/(2*E*I)+Z*c_4+c_3=Z*c_8+c_7\n",
    "(eq4)\t(Z*p)/(E*I)+c_4=c_8\n",
    "(eq5)\tc_1=0\n",
    "\n",
    "(eq6)\t(L^3*c_8)/6+(L^2*c_7)/2+L*c_6+c_5=0\n",
    "\n",
    "(eq7)\tc_3=0\n",
    "\n",
    "(eq8)\tL*c_8+c_7=0\n",
    "(%i17)\t/* Solve system for integration constants */\n",
    "\teq_list: [eq1, eq2, eq3, eq4, eq5, eq6, eq7, eq8]$\n",
    "\tvar_list: [c_1, c_2, c_3, c_4, c_5, c_6, c_7, c_8]$\n",
    "\tconstants: linsolve(eq_list, var_list);      /* 8x8 linear system solution */\n",
    "\t\n",
    "(constants)\t[c_1=0,c_2=((Z^4-4*L*Z^3+4*L^2*Z^2)*p)/(24*E*I*L),c_3=0,c_4=((Z^2-2*L*Z)*p)/(2*E*I*L),c_5=-((Z^4*p)/(24*E*I)),c_6=((Z^4+4*L^2*Z^2)*p)/(24*E*I*L),c_7=-((Z^2*p)/(2*E*I)),c_8=(Z^2*p)/(2*E*I*L)]\n",
    "(%i22)\t/* Apply constants to solutions */\n",
    "\tsol_1A: subst(constants, sol_1A);            /* Final solution segment 1A */\n",
    "\tsol_2A: subst(constants, sol_2A);            /* Final solution segment 2A */\n",
    "\t\n",
    "\t/* ---------------------------- */\n",
    "\t/* PART B: POINT LOAD CASE */\n",
    "\t/* ---------------------------- */\n",
    "\t\n",
    "\t/* 1B: Solve ODE for both segments (unloaded) */\n",
    "\teq: 'diff(u(z), z, 4) = 0;            /* Homogeneous equation */\n",
    "\tsol_1B: desolve(eq, u(z));            /* General solution (same for both segments) */\n",
    "\tsol_2B: sol_1B;                       /* Copy solution for segment 2B */\n",
    "\t\n",
    "(sol_1A)\t(p*z^4)/(24*E*I)+((Z^2-2*L*Z)*p*z^3)/(12*E*I*L)+((Z^4-4*L*Z^3+4*L^2*Z^2)*p*z)/(24*E*I*L)\n",
    "(sol_2A)\t(Z^2*p*z^3)/(12*E*I*L)-(Z^2*p*z^2)/(4*E*I)+((Z^4+4*L^2*Z^2)*p*z)/(24*E*I*L)-(Z^4*p)/(24*E*I)\n",
    "(eq)\t'diff(u(z),z,4)=0\n",
    "\n",
    "(sol_1B)\tu(z)=(z^3*(at('diff(u(z),z,3),z=0)))/6+(z^2*(at('diff(u(z),z,2),z=0)))/2+z*(at('diff(u(z),z,1),z=0))+u(0)\n",
    "(sol_2B)\tu(z)=(z^3*(at('diff(u(z),z,3),z=0)))/6+(z^2*(at('diff(u(z),z,2),z=0)))/2+z*(at('diff(u(z),z,1),z=0))+u(0)\n",
    "(%i24)\t/* Apply initial conditions at z=0 */\n",
    "\tsol_1B: rhs(subst([\n",
    "\t    u(0) = c_1,               /* Deflection at z=0 */\n",
    "\t    'diff(u(z), z, 1) = c_2,  /* Slope at z=0 */\n",
    "\t    'diff(u(z), z, 2) = c_3,  /* Moment at z=0 */\n",
    "\t    'diff(u(z), z, 3) = c_4   /* Shear at z=0 */\n",
    "\t], sol_1B));\n",
    "\t\n",
    "\tsol_2B: rhs(subst([\n",
    "\t    u(0) = c_5,               /* Deflection at z=0 */\n",
    "\t    'diff(u(z), z, 1) = c_6,  /* Slope at z=0 */\n",
    "\t    'diff(u(z), z, 2) = c_7,  /* Moment at z=0 */\n",
    "\t    'diff(u(z), z, 3) = c_8   /* Shear at z=0 */\n",
    "\t], sol_2B));\n",
    "\t\n",
    "(sol_1B)\t(c_4*z^3)/6+(c_3*z^2)/2+c_2*z+c_1\n",
    "(sol_2B)\t(c_8*z^3)/6+(c_7*z^2)/2+c_6*z+c_5\n",
    "(%i32)\t/* Interface conditions at z=W (point load location) */\n",
    "\teq1: subst(W, z, sol_1B) = subst(W, z, sol_2B);              /* Deflection continuity */\n",
    "\teq2: subst(W, z, diff(sol_1B, z, 1)) = subst(W, z, diff(sol_2B, z, 1));  /* Slope continuity */\n",
    "\teq3: subst(W, z, diff(sol_1B, z, 2)) = subst(W, z, diff(sol_2B, z, 2));  /* Moment continuity */\n",
    "\t/* Shear force jump due to point load F */\n",
    "\teq4: -E*I*subst(W, z, diff(sol_1B, z, 3)) + E*I*subst(W, z, diff(sol_2B, z, 3)) = F;\n",
    "\t\n",
    "\t/* Boundary conditions at supports (same as Part A) */\n",
    "\teq5: subst(0, z, sol_1B) = 0;               /* Deflection = 0 at left support */\n",
    "\teq6: subst(L, z, sol_2B) = 0;               /* Deflection = 0 at right support */\n",
    "\teq7: subst(0, z, diff(sol_1B, z, 2)) = 0;   /* Moment = 0 at left support */\n",
    "\teq8: subst(L, z, diff(sol_2B, z, 2)) = 0;   /* Moment = 0 at right support */\n",
    "\t\n",
    "(eq1)\t(W^3*c_4)/6+(W^2*c_3)/2+W*c_2+c_1=(W^3*c_8)/6+(W^2*c_7)/2+W*c_6+c_5\n",
    "(eq2)\t(W^2*c_4)/2+W*c_3+c_2=(W^2*c_8)/2+W*c_7+c_6\n",
    "(eq3)\tW*c_4+c_3=W*c_8+c_7\n",
    "(eq4)\tE*I*c_8-E*I*c_4=F\n",
    "\n",
    "(eq5)\tc_1=0\n",
    "\n",
    "(eq6)\t(L^3*c_8)/6+(L^2*c_7)/2+L*c_6+c_5=0\n",
    "\n",
    "(eq7)\tc_3=0\n",
    "\n",
    "(eq8)\tL*c_8+c_7=0\n",
    "(%i35)\t/* Solve system for integration constants */\n",
    "\teq_list: [eq1, eq2, eq3, eq4, eq5, eq6, eq7, eq8]$\n",
    "\tvar_list: [c_1, c_2, c_3, c_4, c_5, c_6, c_7, c_8]$\n",
    "\tconstants: linsolve(eq_list, var_list);      /* 8x8 linear system solution */\n",
    "\t\n",
    "(constants)\t[c_1=0,c_2=(F*W^3-3*F*L*W^2+2*F*L^2*W)/(6*E*I*L),c_3=0,c_4=(F*W-F*L)/(E*I*L),c_5=-((F*W^3)/(6*E*I)),\n",
    "c_6=(F*W^3+2*F*L^2*W)/(6*E*I*L),c_7=-((F*W)/(E*I)),c_8=(F*W)/(E*I*L)]\n",
    "(%i37)\t/* Apply constants to solutions */\n",
    "\tsol_1B: subst(constants, sol_1B);            /* Final solution segment 1B */\n",
    "\tsol_2B: subst(constants, sol_2B);            /* Final solution segment 2B */\n",
    "\t\n",
    "\t/* ---------------------------- */\n",
    "\t/* NUMERICAL EVALUATION */\n",
    "\t/* ---------------------------- */\n",
    "\t\n",
    "\t/* Define physical parameters */\n",
    "(sol_1B)\t((F*W-F*L)*z^3)/(6*E*I*L)+((F*W^3-3*F*L*W^2+2*F*L^2*W)*z)/(6*E*I*L)\n",
    "(sol_2B)\t(F*W*z^3)/(6*E*I*L)-(F*W*z^2)/(2*E*I)+((F*W^3+2*F*L^2*W)*z)/(6*E*I*L)-(F*W^3)/(6*E*I)\n",
    "(%i42)\tparameters: [p = 3000, F = 50000, Z = 4, W = 7, L = 10];\n",
    "\t\n",
    "\t/* Substitute numerical values */\n",
    "\tsol_1A: ev(sol_1A, parameters);       /* Numerical solution segment 1A */\n",
    "\tsol_1B: ev(sol_1B, parameters);       /* Numerical solution segment 1B */\n",
    "\tsol_2A: ev(sol_2A, parameters);       /* Numerical solution segment 2A */\n",
    "\tsol_2B: ev(sol_2B, parameters);       /* Numerical solution segment 2B */\n",
    "\t\n",
    "(parameters)\t[p=3000,F=50000,Z=4,W=7,L=10]\n",
    "(sol_1A)\t(125*z^4)/(E*I)-(1600*z^3)/(E*I)+(51200*z)/(E*I)\n",
    "(sol_1B)\t(227500*z)/(E*I)-(2500*z^3)/(E*I)\n",
    "(sol_2A)\t(400*z^3)/(E*I)-(12000*z^2)/(E*I)+(83200*z)/(E*I)-32000/(E*I)\n",
    "(sol_2B)\t(17500*z^3)/(3*E*I)-(175000*z^2)/(E*I)+(1452500*z)/(E*I)-8575000/(3*E*I)\n",
    "(%i45)\t/* Post-processing */\n",
    "\tdeflection_at_7_1: ev(sol_1B + sol_2A, z = 7);              /* Total deflection at z=7 */\n",
    "\tdeflection_at_7_2: ev(sol_2A + sol_2B, z = 7);              /* Total deflection at z=7 */\n",
    "\tslope_total: subst(0, z, -diff(sol_1A + sol_1B, z, 1));   /*  Slope at z=0 */\n",
    "(deflection_at_7_1)\t834600/(E*I)\n",
    "(deflection_at_7_2)\t834600/(E*I)\n",
    "(slope_total)\t-(278700/(E*I))\n",
    "\n",
    "\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "language_info": {
   "name": "python"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
